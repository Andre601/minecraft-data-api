/*
 * Copyright (C) 2016 Florian Stober
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

group 'de.codecrafter47.data'
version '1.0.0-SNAPSHOT'

buildscript {
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
    repositories {
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven-publish-auth'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.16.12"
        compileOnly "com.google.code.findbugs:jsr305:3.0.1"
        compileOnly "com.google.guava:guava:18.0"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        repositories {
            maven {
                name "deployment"
                url "http://nexus.codecrafter47.dyndns.eu/content/repositories/snapshots/"
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java

                def path2 = path.replace(':', '.')
                groupId = project(":").group + path2.substring(0, path2.lastIndexOf('.'))
                version = project(":").version

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}
